# 预编译

除了标准的 EVM 预编译之外，Sapphire 还提供了另外三种与密码学相关的预编译，以使某些操作更容易执行且成本更低：x25519 密钥派生和基于 Deoxys-II 的加密和解密。

这些可以通过将调用调度到特定的知名合约地址以与其他预编译相同的方式调用，如下所述。输入参数应该被打包到一个连续的内存区域中，每个数据块像往常一样填充到 32 个字节。

## X25519 密钥派生

### 概述

- 预编译地址：`0x0100000000000000000000000000000000000002`
- 输入格式：`bytes32 public_key || bytes32 private_key`
- 天然气成本：100,000

### 示例

```
function deriveSymmetricKey(bytes32 keyPublic, bytes32 keyPrivate) private view returns (bytes32) {
    bytes32[3] memory data;
    data[0] = keyPublic;
    data[1] = keyPrivate;
    assembly {
        let success := staticcall(gas(), 0x0100000000000000000000000000000000000002, data, 0x40, add(data, 0x40), 0x20)
        if iszero(success) {
            revert(0, 0)
        }
    }
    return data[2];
}
```

## Deoxys-II 加密

### 概述

- 加密预编译地址：`0x0100000000000000000000000000000000000003`
- 解密预编译地址：`0x0100000000000000000000000000000000000004`
- 输入格式：`bytes32 key || bytes32 nonce || uint text_length || uint additional_data_length || text_or_ciphertext || additional_data`
- Gas 成本：最低 50,000 加每字节输入 100

> 警告！ 文本和附加数据均应填充为 32 字节的倍数。

### 示例

```
function encrypt(bytes32 key, bytes32 nonce, bytes memory text, bytes memory additionalData) private view returns (bytes memory result) {
    assembly {
        let p := mload(0x40)
        mstore(p, key)
        mstore(add(p, 0x20), nonce)
        let text_len := mload(text)
        mstore(add(p, 0x40), text_len)
        let ad_len := mload(additionalData)
        mstore(add(p, 0x60), ad_len)
        let i := 0
        for {} lt(i, text_len) { i := add(i, 0x20) } {
            mstore(add(add(p, 0x80), i), mload(add(add(text, 0x20), i)))
        }
        let ad_begin := add(add(p, 0x80), i)
        for { i := 0 } lt(i, ad_len) { i := add(i, 0x20) } {
            mstore(add(ad_begin, i), mload(add(add(additionalData, 0x20), i)))
        }
        mstore(0x40, add(ad_begin, i))
        let out := mload(0x40)
        let success := staticcall(gas(), 0x0100000000000000000000000000000000000003, p, sub(out, p), out, 1)
        if iszero(success) {
            revert(0, 0)
        }
        mstore(out, returndatasize())
        returndatacopy(add(out, 0x20), 0, returndatasize())
        mstore(0x40, add(add(out, 0x20), returndatasize()))
        result := out
    }
}
```

## 资源

上面的例子展示了如何直接调用预编译。为了获得更舒适的体验， Sapphire 存储库、库文件中`contracts/` 的项目中 还提供了包装器。包装器与上面的示例包装器具有相同的签名。

`contracts/Sapphire.sol`

```
pragma solidity ^0.8.13;

import "./Sapphire.sol";

contract Test {
    constructor() {}
    function test() public view returns (bytes32) {
        return Sapphire.deriveSymmetricKey("public key as bytes32", "private key as bytes32");
    }
}
```

> 2022/10/25 - moi